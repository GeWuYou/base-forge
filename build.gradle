
plugins {
    // 用于生成 IntelliJ IDEA 的项目文件，便于在 IDEA 中打开和管理项目
    id 'idea'
    // 应用 Java 插件，提供基本的 Java 代码编译和构建能力
    id 'java'
    // 应用 Java Library 插件，支持库开发，提供 API 和实现分离的能力
    id 'java-library'
    // 应用 Spring Boot 插件，提供 Spring Boot 应用的开发和运行能力
    id 'org.springframework.boot' version '3.3.4'
    // Spring 依赖管理插件，简化依赖版本管理
    id 'io.spring.dependency-management' version '1.1.6'
    // 应用 Protobuf 插件，提供 Protobuf 文件的编译和生成能力
    id 'com.google.protobuf' version '0.9.4'
    // 引入kt
    id 'org.jetbrains.kotlin.jvm' version '2.1.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.1.0'
}
/**
 * 由于 Kotlin 插件被引入时会自动添加依赖，但根项目不需要 Kotlin 依赖，因此需要排除 Kotlin 依赖
 */
configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
}
subprojects {
    idea {
        project {
            jdkName = '21'
            languageLevel = '21'
        }
        module {
            downloadJavadoc = false
            downloadSources = true
        }
    }
    def configDir = "$rootDir/config/"
    apply {
        plugin('idea')
        plugin('java')
        plugin('java-library')
        plugin('org.springframework.boot')
        plugin('com.google.protobuf')
        plugin('io.spring.dependency-management')
        plugin('org.jetbrains.kotlin.jvm')
        plugin('org.jetbrains.kotlin.plugin.spring')
        from(file("$configDir/repositories.gradle"))
        from(file("$configDir/dependencyManagement.gradle"))
    }
    group = 'com.gewuyou'
    version = '0.1.0'
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // region 声明模块
    final base_forge_admin = project(":base-forge-admin")
    final base_forge_admin_api = project(":base-forge-admin-api")
    final base_forge_auth = project(":base-forge-auth")
    // final base_forge_auth_api = project(":base-forge-auth-api")
    final base_forge_user = project(':base-forge-user')
    final base_forge_user_api = project(':base-forge-user-api')
    final base_forge_common = project(":base-forge-common")
    final base_forge_config = project(":base-forge-config")
    final base_forge_discovery = project(":base-forge-discovery")
    final base_forge_file = project(":base-forge-file")
    final base_forge_gateway = project(":base-forge-gateway")
    final base_forge_log = project(":base-forge-log")
    final base_forge_log_api = project(":base-forge-log-api")
    final base_forge_scheduler = project(":base-forge-scheduler")
    final base_forge_search = project(":base-forge-search")
    final base_forge_notification = project(":base-forge-notification")
    final base_forge_notification_api = project(":base-forge-notification-api")
    // endregion


    // region 定义任务对象
    // 复制banner到各个服务模块的资源目录
    Set<Project> NeedCopyBannerResourceProjects = [
            base_forge_admin,
            base_forge_discovery,
            base_forge_file,
            base_forge_gateway,
            base_forge_log,
            base_forge_scheduler,
            base_forge_search,
            base_forge_config,
            base_forge_notification
    ]
    // endregion
    // region 注册任务
    def tasksDir = "$rootDir/config/tasks/"
    if (NeedCopyBannerResourceProjects.contains(project)) {
        apply {
            from("${tasksDir}/bannerTask.gradle")
        }
    }
    // endregion
    // region 设置子模块需要的依赖
    // 设置需要 actuator 依赖的模块
    Set<Project> ActuatorProjects = [
            base_forge_gateway,
    ]
    // 设置需要grpc基本依赖的模块
    Set<Project> GrpcBaseProjects = [
            base_forge_common,
    ]
    // 设置需要grpc客户端依赖的模块
    Set<Project> GrpcClientProjects = [
            base_forge_log_api,
            base_forge_admin_api
    ]
    // 设置需要grpc服务端依赖的模块
    Set<Project> GrpcServerProjects = [
            // 9090
            base_forge_admin,
            // 9091
            base_forge_log,
            // 9092
            base_forge_config,
            // 9093

    ]
    // 设置需要公共模块依赖的模块
    Set<Project> CommonProjects = [
            base_forge_admin,
            base_forge_admin_api,
            base_forge_config,
            base_forge_discovery,
            base_forge_file,
            base_forge_gateway,
            base_forge_log,
            base_forge_log_api,
            base_forge_scheduler,
            base_forge_search,
            base_forge_notification,
            base_forge_notification_api
    ]
    // 设置需要导入Admin api依赖的模块
    Set<Project> AdminProjects = [
            base_forge_gateway
    ]
    // 设置需要启用配置提示处理的模块
    Set<Project> ConfigProcessorProjects = [
            base_forge_admin_api
    ]
    // 设置需要导入Swagger依赖的模块
    Set<Project> SwaggerProjects = [
            base_forge_common,
            base_forge_notification,
            base_forge_scheduler,
            base_forge_user,
    ]
    // 设置需要导入日志服务的模块
    Set<Project> LogProjects = [
    ]
    // 设置需要导入配置服务的模块
    Set<Project> ConfigProjects = [
    ]
    // 设置需要导入MybatisPlus依赖的模块
    Set<Project> MybatisPlusProjects = [
            base_forge_common,
            base_forge_log,
            base_forge_config,
            base_forge_scheduler,
            base_forge_admin,
            base_forge_user
    ]
    // 设置需要导入mybatisStarter依赖的模块
    Set<Project> MybatisExtensionStarterProjects = [
            base_forge_user,
    ]
    // 设置需要导入Jpa依赖的模块
    Set<Project> JpaProjects = [
            base_forge_user,
    ]

    // 设置需要导入postgresql依赖的模块
    Set<Project> PostgresqlProjects = [
            base_forge_user,
    ]

    // 设置需要导入尤里卡客户端依赖的模块
    Set<Project> EurekaClientProjects = [
            base_forge_admin,
            base_forge_file,
            base_forge_gateway,
            base_forge_log,
            base_forge_scheduler,
            base_forge_search,
            base_forge_notification,
            base_forge_user,
            base_forge_auth
    ]
    // 设置需要导入配置中心客户端依赖的模块
    Set<Project> ConfigClientProjects = [
            base_forge_admin,
            base_forge_discovery,
            base_forge_file,
            base_forge_gateway,
            base_forge_log,
            base_forge_scheduler,
            base_forge_search,
            base_forge_notification,
            base_forge_user,
            base_forge_auth
    ]
    // 设置需要导入openfeign依赖的模块
    Set<Project> OpenFeignProjects = [
            base_forge_notification_api,
            base_forge_admin_api,
            base_forge_log_api,
            base_forge_user_api
    ]
    // 设置需要导入rabbitmq依赖的模块
    Set<Project> RabbitmqProjects = [
            base_forge_notification,
            base_forge_notification_api
    ]
    // 设置需要导入security core依赖的模块
    Set<Project> SecurityCoreProjects = [
            base_forge_admin,
            base_forge_admin_api,
            base_forge_gateway,
            base_forge_common
    ]

    // region base-forge-spring-boot-starter
    // 设置需要导入webStarter依赖的模块
    Set<Project> WebStarterProjects = [
            base_forge_admin,
            base_forge_config,
            base_forge_discovery,
            base_forge_file,
            base_forge_gateway,
            base_forge_log,
            base_forge_scheduler,
            base_forge_search,
            base_forge_notification,
            base_forge_user,
            base_forge_auth
    ]
    // 设置需要导入redisStarter依赖的模块
    Set<Project> RedisStarterProjects = [

    ]
    // 设置需要导入security 认证和鉴权 starter依赖的模块
    Set<Project> SecurityStarterProjects = [
            base_forge_auth
    ]
    // 设置需要导入grpcStarter依赖的模块
    Set<Project> GrpcStarterProjects = [
            base_forge_admin_api,
            base_forge_log_api,
            base_forge_notification_api
    ]
    // 设置需要打包成可执行jar的模块
    Set<Project> ExecutableJarProjects = [
            base_forge_admin,
    ]
    if (ExecutableJarProjects.contains(project)) {
        apply {
            from("${configDir}/bulid.gradle")
        }
    }

    // endregion
    // region 设置依赖
    dependencies {
        // region 文档注释
        /*
         implementation：
         用于声明项目的内部依赖。其他模块无法访问这些依赖。
         优先用于代码中的实现逻辑。
         适合大多数场景。

         api：
         用于声明库的公共 API 依赖。其他模块可以访问这些依赖。
         适合用在库项目中，确保依赖能够被外部使用。

         compileOnly：
         用于声明编译时依赖，但不会在运行时包含这些依赖。
         通常用于需要在编译时可用但在运行时不需要的依赖（如某些注解处理器）。

         runtimeOnly：
         用于声明在运行时需要，但在编译时不需要的依赖。
         例如，某些数据库驱动可能在编译时不需要，但在运行时需要。

         testImplementation：
         用于声明测试代码的依赖。这些依赖仅在测试代码中可用。
         类似于 implementation，但仅适用于测试。

         testCompileOnly 和 testRuntimeOnly：
         分别用于在测试时的编译和运行时依赖，具有与 compileOnly 和 runtimeOnly 相同的意义，但只适用于测试。

         provided（在某些插件中）：
         用于声明在编译时需要，但在运行时由容器提供的依赖（如某些 Web 应用）。

         enforcedPlatform：
         用于定义一个 BOM（Bill of Materials），确保所有相关的依赖使用相同的版本。

         platform：
         类似于 enforcedPlatform，但用于声明可以共享版本管理的依赖集合。
         */
        // endregion
        // region 设置公共的依赖
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testCompileOnly('org.projectlombok:lombok')
        testAnnotationProcessor 'org.projectlombok:lombok'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        // endregion
        // region 设置导入依赖的逻辑
        if (ActuatorProjects.contains(project)) {
            // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-actuator
            implementation 'org.springframework.boot:spring-boot-starter-actuator'

        }
        if (GrpcStarterProjects.contains(project)) {
            implementation('com.gewuyou:base-forge-grpc-spring-boot-starter')
        }
        if (WebStarterProjects.contains(project)) {
            implementation('com.gewuyou:base-forge-web-spring-boot-starter-autoconfigure')
            implementation('com.gewuyou:base-forge-web-spring-boot-starter')
        }
        if (SecurityCoreProjects.contains(project)) {
            // https://mvnrepository.com/artifact/org.springframework.security/spring-security-core
            implementation 'org.springframework.security:spring-security-core'

        }
        if (ConfigClientProjects.contains(project)) {
            // 添加SpringConfig服务依赖
            implementation('org.springframework.cloud:spring-cloud-starter-config')
            implementation('org.springframework.cloud:spring-cloud-starter-bus-amqp')
        }
        if (EurekaClientProjects.contains(project)) {
            // 添加Eureka服务依赖
            implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
        }
        if (ConfigProcessorProjects.contains(project)) {
            // 添加配置提示处理依赖
            annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        }
        if (SwaggerProjects.contains(project)) {
            implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
        }
        if (CommonProjects.contains(project)) {
            implementation base_forge_common
        }
        if (GrpcBaseProjects.contains(project)) {
            implementation("io.grpc:grpc-netty")
            implementation("io.grpc:grpc-protobuf")
            implementation("io.grpc:grpc-stub")
            implementation('org.apache.tomcat:annotations-api')
        }
        if (GrpcClientProjects.contains(project)) {
            // gRPC核心库
            runtimeOnly("io.grpc:grpc-netty-shaded")
            implementation("io.grpc:grpc-netty")
            implementation("io.grpc:grpc-protobuf")
            implementation("io.grpc:grpc-stub")
            implementation("io.grpc:grpc-services")

            implementation('org.apache.tomcat:annotations-api')
            // Spring Boot Starter 服务端依赖
            implementation('net.devh:grpc-client-spring-boot-starter')
            // 序列化库
            implementation("com.google.protobuf:protobuf-java")
            implementation("com.google.protobuf:protobuf-java-util")
        }
        if (GrpcServerProjects.contains(project)) {
            // gRPC核心库
            runtimeOnly("io.grpc:grpc-netty-shaded")
            implementation("io.grpc:grpc-netty")
            implementation("io.grpc:grpc-protobuf")
            implementation("io.grpc:grpc-stub")
            implementation("io.grpc:grpc-services")

            implementation('org.apache.tomcat:annotations-api')
            // Spring Boot Starter 服务端依赖
            implementation('net.devh:grpc-server-spring-boot-starter')
            // 序列化库
            implementation("com.google.protobuf:protobuf-java")
            implementation("com.google.protobuf:protobuf-java-util")
        }
        if (AdminProjects.contains(project)) {
            implementation base_forge_admin_api
        }
        if (LogProjects.contains(project)) {
            implementation base_forge_log_api
        }
        if (MybatisPlusProjects.contains(project)) {
            // https://mvnrepository.com/artifact/com.baomidou/mybatis-plus-spring-boot3-starter
            implementation 'com.baomidou:mybatis-plus-spring-boot3-starter'
        }
        if (OpenFeignProjects.contains(project)) {
            // https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-starter-openfeign
            implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        }
        if (RabbitmqProjects.contains(project)) {
            implementation 'org.springframework.boot:spring-boot-starter-amqp'
            testImplementation 'org.springframework.amqp:spring-rabbit-test'
        }
        if (PostgresqlProjects.contains(project)) {
            // https://mvnrepository.com/artifact/org.postgresql/postgresql
            implementation 'org.postgresql:postgresql'
        }
        if (JpaProjects.contains(project)) {
            implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        }
        if(MybatisExtensionStarterProjects.contains(project)){
            implementation('com.gewuyou:base-forge-mybatis-extension-spring-boot-starter')
        }
        if(SecurityStarterProjects.contains(project)){
            implementation('com.gewuyou:base-forge-security-authentication-spring-boot-starter')
            implementation('com.gewuyou:base-forge-security-authentication-spring-boot-starter-autoconfigure')
            implementation('com.gewuyou:base-forge-security-authorization-spring-boot-starter')
            implementation('com.gewuyou:base-forge-security-authorization-spring-boot-starter-autoconfigure')
        }
        // endregion
    }
    // endregion
    tasks.named('test') {
        useJUnitPlatform()
    }
}





